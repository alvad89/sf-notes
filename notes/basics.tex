\documentclass[12pt,a4paper,draft]{article}
\usepackage[utf8x]{inputenc}
\usepackage[english,russian]{babel}
\usepackage{ucs}
\usepackage{amsmath}
\usepackage{amsfonts}
\usepackage{amssymb}
\usepackage{verbatim}
\begin{document}
\section{Enumerated Types}

В языке Coq практически нет никаких встроенных батареек -- ни логических значений, ни чисел. Coq предоставляет мощные стредства для определения новых типов данных и функций, которые оперируют и трансформируют эти типы.

\subsection{Days of the Week}
Рассмотрим простой пример:

\begin{verbatim}
Inductive day : Type :=
  | monday : day
  | tuesday : day
  | wednesday : day
  | thursday : day
  | friday : day
  | saturday : day
  | sunday : day.
\end{verbatim}

Это объявление говорит Coq, что мы определяем новый набор значений данных -- "тип". Этот тип назвали \texttt{day}, а значения этого типа -- \texttt{monday}, \texttt{tuesday} и т.д.

После того, как мы определили тип, мы можем построить функцию над значениями этого типа:

\begin{verbatim}
Definition next_weekday (d:day) : day :=
  match d with
  | monday => tuesday
  | tuesday => wednesday
  | wednesday => thursday
  | thursday => friday
  | friday => monday
  | saturday => monday
  | sunday => monday
  end.
\end{verbatim}

В Coq есть механизм вывода типов, который почти всегда может вывести тип, но для простоты понимания лучше всегда указывать сигнатуры.

После того, как мы определили функцию, самое время проверить ее работоспособность на нескольких примерах. В Coq есть три способа сделать это. Во-первых, мы можем использовать команду \texttt{Eval simpl}:

\begin{verbatim}
Eval simpl in (next_weekday friday).
   (* ==> monday : day *)
Eval simpl in (next_weekday (next_weekday saturday)).
   (* ==> tuesday : day *)
\end{verbatim}

Во-вторых, мы можем явно указать какой результат вы ожидаем:

\begin{verbatim}
Example test_next_weekday:
  (next_weekday (next_weekday saturday)) = tuesday.
\end{verbatim}

После этого мы можем поручить Coq проверить это утверждение:

\begin{verbatim}
Proof. simpl. reflexivity. Qed.
\end{verbatim}

Третий способ заключается в экстракции определения в какой-нибудь язык программирования (OCaml, Scheme или Haskell). Это очень крутая возможность т.к. на выходе получится полностью корректная (fully certified) программа. Это одна из основых фич системы Coq, ради которой все и разрабатывалось.

\subsection{Booleans}

Аналогично мы можем определить тип \texttt{bool} для логических значений:

\begin{verbatim}
Inductive bool : Type :=
  | true : bool
  | false : bool.
\end{verbatim}

В стандартной библиотеке Coq есть определения и леммы для логических значений (\texttt{Coq.Init.Datatypes}), но мы будем делать все руками в академических целях.

Построим несколько полезных функций над значениями типа \texttt{bool}:

\begin{verbatim}
Definition negb (b:bool) : bool :=
  match b with
  | true => false
  | false => true
  end.

Definition andb (b1:bool) (b2:bool) : bool :=
  match b1 with
  | true => b2
  | false => false
  end.

Definition orb (b1:bool) (b2:bool) : bool :=
  match b1 with
  | true => true
  | false => b2
  end.
\end{verbatim}

Мы можем определять и функции нескольких переменных. Теперь напишем 4 "юнит-теста", которые составляют полную спецификацию -- таблицу истинности -- для функции \texttt{orb}:

\begin{verbatim}
Example test_orb1: (orb true false) = true.
Proof. simpl. reflexivity. Qed.
Example test_orb2: (orb false false) = false.
Proof. simpl. reflexivity. Qed.
Example test_orb3: (orb false true) = true.
Proof. simpl. reflexivity. Qed.
Example test_orb4: (orb true true) = true.
Proof. simpl. reflexivity. Qed.
\end{verbatim}

\section{Function Types}
В Coq есть команда \texttt{Check}, которая печатает тип выражения.

\begin{verbatim}
Check true.
(* ===> true : bool *)
Check (negb true).
(* ===> negb true : bool *)
\end{verbatim}

Функции тоже имеют свой тип:

\begin{verbatim}
Check negb.
(* ===> negb : bool -> bool *)
\end{verbatim}

Из этой записи видно, что \texttt{negb} принимает на вход одно значение типа \texttt{bool} и возвращает значение типа \texttt{bool}. У функций нескольких переменных сигнатура будет иметь примерно такой вид:

\begin{verbatim}
Check andb.
(* ===> andb : bool -> bool -> bool *)
\end{verbatim}

Это одначает, что функция принимает на вход два аргумента типа \texttt{bool} и возвращает значение типа \texttt{bool}.

\section{Numbers}

В Coq есть механизм модулей, который позволяет не засорять пространство имен в больших проектах:

\begin{verbatim}
Module Playground1.

Inductive nat : Type :=
  | O : nat
  | S : nat → nat.
\end{verbatim}

В примере выше показан еще один способ определения типа с помощью набора индуктивных правил. Этот пример следует читать так:
\begin{itemize}
	\item O -- это натуральное число,
	\item S -- это конструктор, который принимает натуральное число и возвращает еще одно натуральное число, таким образом, если $n \in \mathbb{N}$, то и $S\, n \in \mathbb{N}$.
\end{itemize}

Каждое множество, определенное по индукции (\texttt{weekday}, \texttt{bool}, \texttt{nat} и т.п.) представляет собой набор выражений. Определение \texttt{nat} описывает то, как множество может быть построено.

Мы можем построить простую функцию сопоставления с образцом аналогичным образом:

\begin{verbatim}
Definition pred (n : nat) : nat :=
  match n with
    | O => O
    | S n' => n'
  end.
\end{verbatim}

Вторую ветку следует читать так: "если $n$ имеет форму $S \, n'$ для некоторого $n'$, то возвращаем $n'$".

Определение рекурсивной функции:

\begin{verbatim}
Fixpoint evenb (n:nat) : bool :=
  match n with
  | O => true
  | S O => false
  | S (S n') => evenb n'
  end.
\end{verbatim}

Разумеется, можно определять рекурсивные функции нескольких переменных:

\begin{verbatim}
Fixpoint plus (n : nat) (m : nat) : nat :=
  match n with
    | O => m
    | S n' => S (plus n' m)
  end.
\end{verbatim}

\section{Proof by Simplification}
Теперь, когда мы определили несколько типов данных и функций над ними, давайте обратимся к вопросу о том, как сформулировать и доказать их свойства и поведение. На самом деле, в некотором смысле, мы уже начали это делать: каждый \texttt{Example} в предыдущих разделах содержал некоторые утверждения о поведении некоторых функций, приминяемых к определенному набору аргументов. Доказательства этих утверждений всегда были одними и теми же -- с помощью определения функций мы упрощали выражения и убеждались в том, что левые и правые части выражения идентичны.

Такого же рода "доказательства путем упрощения" могут быть использованы для доказательства более прикольных вещей. Докажем, что 0 является нейтральным элементом для операции сложения:

\begin{verbatim}
Theorem plus_O_n : forall n:nat, 0 + n = n.
Proof.
  simpl. reflexivity. Qed.
\end{verbatim}

Команда \texttt{reflexivity} неявно упрощает обе стороны выражения перед сравнением.

Ключевые слова \texttt{simpl} и \texttt{reflexivity} являются примерами тактик. Цель тактики -- сказать Coq, каким образом должна быть проведена корректность некоторых утверждений.

\section{The \texttt{intros} Tactic}

Помимо юнит-тестов, которые применяют функции к аргументам, мы будем заинтересованы в доказательстве свойств программ, формулировка которых начинается к некоторых кванторов (например, для всех чисел n и т.д.) и/или гипотез (например, пусть $n = m$).

Тактика \texttt{intros} позволяет нам перенести кванторы или гипотезы из утверждения в контекст текущих рассуждений.

\begin{verbatim}
Theorem plus_O_n'' : forall n : nat, 0 + n = n.
Proof. intros n. reflexivity. Qed.

Theorem plus_1_l : forall n : nat, 1 + n = S n.
Proof. intros n. reflexivity. Qed.
\end{verbatim}

Суффикс \texttt{\_l} в этих теоремах произностится как "слева".

\section{Proof by Rewriting}

Рассмотрим более интересный пример:

\begin{verbatim}
Theorem plus_id_example : forall n m : nat,
n = m ->
n + m = m + n.
\end{verbatim}

Вместо того, чтобы формулировать польностью универсальные утверждения о всех числах $n$ и $m$, эта теорема говорит о более узких свойствах, что имеют место при $n=m$. Символ стрелки произносится как "следует". Поскольку $n$ и $m$ -- произвольные числа, мы не можем использовать упрощения для доказательства этой теоремы. Вместо этого мы докажем, что заменяя $n$ на $m$ в условии (в предположении $n=m$), мы получим верное тождество. Тактика, указывающая Coq соверщить такую замену, называется \texttt{rewrite}.

\begin{verbatim}
Proof.
 intros n m.
 intros H.
 rewrite -> H.
 reflexivity. Qed.
\end{verbatim}

Первая строка доказательства перемещает оба квантора в контекст, вторая заталкивает туда гипотезу, третья говорит Coq переписать текущую цель($n + n = m + m$) заменой левой части равенства гипотезы $H$ правой частью.

\section{Case Analysis}

Разумеется далеко не все можно доказать простыми вычислениями: например произвольные числа, логические значения, списки и т.п. могут составлять определение функции, блокируя тем самым упрощение. Использование тактики \texttt{simpl} в следующем примере бесполезно:

\begin{verbatim}
Theorem plus_1_neq_0_firsttry : forall n : nat,
  beq_nat (n + 1) 0 = false.
Proof.
  intros n. simpl. (* does nothing! *)
Admitted.
\end{verbatim}

В этом случае кошерно использовать тактику \texttt{destruct}, которая разделит доказательство на две независимые ветви для $n=0$ и $n = S \, n'$:

\begin{verbatim}
Theorem plus_1_neq_0 : forall n : nat,
  beq_nat (n + 1) 0 = false.
Proof.
  intros n. destruct n as [| n'].
    reflexivity.
    reflexivity. Qed.
\end{verbatim}


\section{Naming Cases}

Отсутсвие в Coq явной команды для перехода из одной ветви доказательства в другую может превратить стройное доказательство в нечитаемое месиво, которое трудно воспринимать и разбирать. Расстановка отступов и комментарии могут несколько улучшить ситуацию, но лучше использовать тактику \texttt{Case}. Тактика \texttt{Case} не встроена в Coq, поэтому ее нужно определить самому:

\begin{verbatim}
Require String. Open Scope string_scope.

Ltac move_to_top x :=
  match reverse goal with
  | H : _ |- _ => try move x after H
  end.

Tactic Notation "assert_eq" ident(x) constr(v) :=
  let H := fresh in
  assert (x = v) as H by reflexivity;
  clear H.

Tactic Notation "Case_aux" ident(x) constr(name) :=
  first [
    set (x := name); move_to_top x
  | assert_eq x name; move_to_top x
  | fail 1 "because we are working on a different case" ].

Tactic Notation "Case" constr(name) := Case_aux Case name.
Tactic Notation "SCase" constr(name) := Case_aux SCase name.
Tactic Notation "SSCase" constr(name) := Case_aux SSCase name.
\end{verbatim}

Пример использования:

\begin{verbatim}
Theorem andb_true_elim1 : forall b c : bool,
  andb b c = true -> b = true.
Proof.
  intros b c H.
  destruct b.
  Case "b = true".
    reflexivity.
  Case "b = false".
    rewrite <- H. reflexivity. Qed.
\end{verbatim}





















\end{document}